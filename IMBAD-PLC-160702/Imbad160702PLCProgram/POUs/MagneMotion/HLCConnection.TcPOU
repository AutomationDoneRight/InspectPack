<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.6">
  <POU Name="HLCConnection" Id="{125cb7c1-b994-4f44-a852-a004d09f2934}" SpecialFunc="None">
    <Declaration><![CDATA[(* This function block provides a socket and connection to the Magnemotion HLC *)
(* TxFifo is checked for any packets to send, if there are any, this fb will manage sending them *)
(* Any received data is properly checked for length and inserted into the RxFifo tail *)
FUNCTION_BLOCK PUBLIC HLCConnection
VAR_IN_OUT
	fbTx 		: FB_Tx_FrameFifo;(* TX fifo *)
	fbRx 		: FB_Rx_FrameFifo;(* RX fifo *)
	fbLog		: FB_LogFifo;(* Log message fifo *)
END_VAR

VAR_INPUT
	sSrvNetID	: T_AmsNetID	:= '';(* TwinCAT TCP/IP server network address *)
	sRemoteHost	: T_IPv4Addr 	:= '192.168.0.61';(* IP address of remote server *)
	nRemotePort	: UDINT			:= 799;(* Remote server port *)
	bEnable		: BOOL			:= FALSE;(* TRUE => Enable/connect, FALSE => Disable/disconnect *)
	tReconnect	: TIME 			:= T#10S;(* Try to reconnect after this time *)
	bLog		: BOOL 			:= FALSE; (* TRUE => Enable debug output, FALSE => Disable debug output *)
	sPrefix		: STRING 		:= 'CLIENT::';(* Log message description string (allows the identification of log message source) *)
END_VAR

VAR_OUTPUT
	eState		: E_SocketConnectionState := eSOCKET_DISCONNECTED;(* TCP/IP connection state *)
END_VAR

VAR
	fbConnect 	: FB_ClientServerConnection;(* Create/release TCP/IP connection *)
	fbSend		: FB_SocketSend;(* Send TCP/IP data *)
	fbReceive	: FB_SocketReceive;(* Receive TCP/IP data *)
	nState 		: BYTE;(* Global state *)
	bSndBusy 	: BOOL;(* Tx state *)
	bRcvBusy 	: BOOL;(* Rx state *)
	bDisconnect	: BOOL;(* Disconnect flag, if set the socket will be closed *)

	fbPollTimer	: TON;(* Rx data poll timer *)
	fbResponseTimer	: TON;(* Response timeout timer *)
	fbReconnectTimer : TON;(* Reconnect timer *)

	nS			: UDINT;(* Send frame counter *)
	nR			: UDINT;(* Received frame counter *)
	cbReceived	: UDINT;(* Count of received data bytes *)
	rxBuffer	: ARRAY[0..MM_CONST.PLCPRJ_BUFFER_SIZE] OF BYTE;
	cbFrame		: UDINT;(* Rx frame byte size *)
	stTxFrame	: ST_MM_TxFrame;(* Tx frame buffer (inclusive header) *)
	stRxFrame	: ST_MM_RxFrame;(* Rx frame buffer (inclusive header) *)
	bAbort		: BOOL;(* Flag to abort frame parsing loop *)
	event		: FB_SimpleAdsLogEvent;
	
	bInitialize : BOOL := TRUE;
	fbCloseAll		: FB_SocketCloseAll;
	
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[fbCloseAll( sSrvNetID := sSrvNetID, bExecute:= bInitialize );
IF fbCloseAll.bBusy OR fbCloseAll.bError THEN
	RETURN;
END_IF
bInitialize := FALSE;
CASE nState OF
	0:(* Create TCP/IP connection *)
		fbConnect( 	sSrvNetID 		:= sSrvNetID,
					nMode			:= SEL( bLog, 0, CONNECT_MODE_ENABLEDBG ),(* Enable/disable debug output *)
					sRemoteHost 	:= sRemoteHost,
					nRemotePort 	:= nRemotePort,
					tReconnect		:= tReconnect,
					bEnable			:= bEnable,
					eState			=> eState );
		IF NOT fbConnect.bBusy THEN
			IF NOT fbConnect.bError THEN
				IF eState = eSOCKET_CONNECTED THEN(* we are connected *)
					
					fbTx.Clear( sPrefix := CONCAT( sPrefix, 'FB_Tx_FrameFifo(sender)::' ), bLog := bLog );(* Clear TX fifo (optional) *)
					fbRx.Clear( sPrefix := CONCAT( sPrefix, 'FB_Rx_FrameFifo(receiver)::' ), bLog := bLog );(* Clear RX fifo (optional) *)
					fbLog.Clear( sPrefix := sPrefix, bLog := bLog );(* Clear log fifo (optional) *)
					bSndBusy := FALSE;
					bRcvBusy := FALSE;
					bDisconnect := FALSE;
					cbReceived 	:= 0;(* Reset number of received data bytes *)
					nR 			:= 0;(* Reset receive frame counter *)
					nS 			:= 0;(* Reset send frame counter *)

					fbPollTimer( IN := FALSE, PT := T#0S );
					fbResponseTimer( IN := FALSE, PT := MM_CONST.PLCPRJ_RECEIVE_TIMEOUT );
					fbReconnectTimer( IN := FALSE, PT := tReconnect );
				    //event(SourceID := 17001,EventID:=1,bSetEvent := TRUE, bQuit :=FALSE); 
					fbLog.Message( sMessage := 'Connection established!', hSocket := fbConnect.hSocket );
					nState 	:= 1;
				END_IF
			ELSE(* Connect error => log error *)
				fbLog.Error( sMessage := 'FB_ClientServerConnection()', nErrID := fbConnect.nErrId );
				 //event(SourceID := 17001,EventID:=1,bSetEvent := TRUE, bQuit :=FALSE); 
			END_IF
		END_IF


	1:(* Data exchange state *)
		bDisconnect := NOT bEnable OR bDisconnect;(* User/internal disconnect requested? *)
		IF bDisconnect AND NOT bSndBusy AND NOT bRcvBusy THEN
			bDisconnect := FALSE;
			fbLog.Message( sMessage := 'Closing connection!', hSocket := fbConnect.hSocket );
			nState := 2;(* Disconnect *)
		ELSE

			(* --------------------------- Send tx data --------------------------------- *)
			IF NOT bDisconnect AND NOT bSndBusy THEN
				fbTx.RemoveHead();(* Remove oldest user data entry *)
				IF fbTx.bOk THEN(* Success *)
						
					stTxFrame := fbTx.stGet;
					(* Properly formatted message is expected to be entered into the FIFO*)				
					(* Send frame *)
					fbSend( bExecute := FALSE );
					fbSend(	sSrvNetID 	:= sSrvNetID,
							hSocket		:= fbConnect.hSocket,
							cbLen		:= MM_CONST.TOTAL_MSG_LEN_ADJUST + stTxFrame.MsgLength,(* Size of header + size of user data *)
							pSrc		:= ADR(stTxFrame),(* Pointer to the frame data *)
							bExecute	:= TRUE );
					bSndBusy := TRUE;
				END_IF
			ELSE(* Wait until send not busy *)
				fbSend( bExecute := FALSE );
				IF NOT fbSend.bBusy THEN
					bSndBusy := FALSE;
					IF NOT fbSend.bError THEN(* No error *)
						nS := nS + 1;(* Increment the number of send frames *)
					ELSE(* Send error => Log error and disconnect *)
						fbLog.Error( sMessage := 'FB_SocketSend()', nErrID := fbSend.nErrId );
						bDisconnect := TRUE;(* Set flag *)
					END_IF
				END_IF
			END_IF
			(* -------------------------------------------------------------------------- *)

			(* ------------------ Get/fetch rx data ------------------------------------- *)
			IF NOT bDisconnect AND NOT bRcvBusy THEN
				fbPollTimer( IN := TRUE );
				IF fbPollTimer.Q THEN
					IF fbRx.cbFree >= SIZEOF(stRxFrame) THEN(* Check free rx fifo space *)
						fbPollTimer( IN := FALSE, PT := MM_CONST.PLCPRJ_RECEIVE_POLLING_TIME );
						fbReceive( bExecute := FALSE );
						fbReceive( 	sSrvNetId	:= sSrvNetID,
									hSocket		:= fbConnect.hSocket,
									cbLen		:= SIZEOF(rxBuffer) - cbReceived,
									pDest		:= ADR(rxBuffer) + cbReceived,
									bExecute	:= TRUE );
						bRcvBusy := TRUE;
					END_IF
				END_IF
				
			ELSE(* Wait until receive not busy *)
				
				fbReceive( bExecute := FALSE );
				IF NOT fbReceive.bBusy THEN
					bRcvBusy := FALSE;
					IF NOT fbReceive.bError THEN
						IF fbReceive.nRecBytes > 0 THEN (* Some bytes received *)

							fbPollTimer( IN := FALSE, PT := T#0S );(* Bytes received => increase the read (polling) speed *)
							fbResponseTimer( IN := FALSE );(* Bytes received => disable timeout check *)
							cbReceived := cbReceived + fbReceive.nRecBytes;

							(* Parse received bytes *)
							REPEAT
								bAbort := TRUE;(* Set flag *)
								
								IF (cbReceived >= MM_CONST.FRAMING_HEADER) THEN (* All header bytes received => check header data *)
									
									MEMCPY( ADR( stRxFrame ), ADR( rxBuffer ),MM_CONST.FRAMING_HEADER  );(* Get header data *)
								
									IF (stRxFrame.StartBytes = HOST_TO_BE16(16#ABBA)) (* Check start bytes *)
									AND (stRxFrame.MsgLength <= SIZEOF(stRxFrame))(* Check frame (user data) length *)
									THEN
									
										cbFrame := stRxFrame.MsgLength+MM_CONST.FRAMING_HEADER;(* Calculate the expected receive frame length *)
										IF cbReceived >= cbFrame  THEN (* All frame (user data) bytes received? *)
	
											MEMCPY( ADR( stRxFrame ), ADR( rxBuffer ), MIN( SIZEOF(stRxFrame), cbFrame ) );
											fbRx.AddTail( stPut := stRxFrame);(* Add user data to the rx fifo *)
											IF fbRx.bOk THEN
												nR := nR + 1;(* Increment receive counter *)

												MEMMOVE( ADR( rxBuffer ), ADR( rxBuffer ) + cbFrame, cbReceived - cbFrame );(* Shift the remaining rx bytes *)
												cbReceived := cbReceived - cbFrame;(* Recalculate the size of the remaining rx bytes *)

												IF fbRx.cbFree >= SIZEOF(stRxFrame) THEN
													bAbort := FALSE;(* reset flag, try to parse next frame *)
												END_IF
											ELSE (* Fifo overflow => log error and disconnect *)
												fbLog.Error( sMessage := 'FB_FrameFifo(receiver)', nErrID := MM_CONST.PLCPRJ_ERROR_RECFIFO_OVERFLOW );
												bDisconnect := TRUE;(* Set flag *)
												RETURN;
											END_IF

(*										ELSE repeat reading *)
										END_IF

									ELSE(* Receive error => Invalid frame format/data *)
										fbLog.Error( sMessage := 'FB_ClientConnection()', nErrID := MM_CONST.PLCPRJ_ERROR_INVALID_FRAME_FORMAT );
										bDisconnect := TRUE;(* Set flag *)
										RETURN;
									END_IF

(*								ELSE repeat reading *)
								END_IF (* IF cbReceived >= SIZEOF(stRxFrame.head) THEN *)

							UNTIL bAbort
							END_REPEAT

						ELSE(* No bytes received *)
							fbResponseTimer( IN := TRUE );(* No bytes received => enable timeout check *)
							IF fbResponseTimer.Q THEN (* Timeout error*)
								fbResponseTimer( IN := FALSE );
								fbLog.Error( sMessage := 'FB_ClientConnection()', nErrID := MM_CONST.PLCPRJ_ERROR_RECEIVE_TIMEOUT );
								bDisconnect := TRUE;(* Set flag *)
							END_IF							
						END_IF(* IF fbReceive.nRecBytes > 0 THEN *)

					ELSE(* Receive error => Log error and disconnect *)
						fbLog.Error( sMessage := 'FB_SocketReceive()', nErrID := fbReceive.nErrId );
						bDisconnect := TRUE;(* set flag *)
					END_IF

				END_IF(* IF NOT fbReceive.bBusy THEN *)
				
			END_IF(* IF NOT bDisconnect AND NOT bRcvBusy THEN *)
			(* -------------------------------------------------------------------------- *)
			
			
		END_IF(* IF bDisconnect AND NOT bSndBusy AND NOT bRcvBusy THEN *)


	2:(* Disconnect *)
		fbConnect( bEnable:= FALSE, eState=>eState );
		IF eState = eSOCKET_DISCONNECTED THEN
			fbReconnectTimer( IN := TRUE );(* Please wait some seconds and then try to connect again *)
			IF fbReconnectTimer.Q THEN
				fbReconnectTimer( IN := FALSE );
				//event(SourceID := 17001,EventID:=2,bSetEvent := TRUE, bQuit :=FALSE); 
				nState := 0;
			END_IF
		END_IF


END_CASE]]></ST>
    </Implementation>
    <LineIds Name="HLCConnection">
      <LineId Id="239" Count="3" />
      <LineId Id="238" Count="0" />
      <LineId Id="52" Count="111" />
      <LineId Id="165" Count="72" />
      <LineId Id="9" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>